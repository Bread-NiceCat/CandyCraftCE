plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    forge()
}
def generatedResources = new File(project(":common").getProjectDir(), "/src/main/generated")

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath
    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
        mixinConfig "candycraftce-common.mixins.json"
        mixinConfig "candycraftce.mixins.json"
    }

    runs {
        data {
            data()
            name("Forge Data Generator")

            programArgs "--all", "--mod", "candycraftce"
            programArgs "--output", generatedResources.absolutePath
            programArgs "--existing", new File(project(":common").getProjectDir(), "/src/main/resources").absolutePath
        }
        client {
            vmArg "-Dmixin.debug.export=true"
//            vmArg "-Dmixin.debug.verbose=true"
        }
    }
    if (Boolean.parseBoolean(enable_jbr)) {
        runs.forEach {
            it.vmArg "-XX:+AllowEnhancedClassRedefinition"
        }
    }
}
configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
//    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"


    modRuntimeOnly("curse.maven:appleskin-248787:4770828")

    modRuntimeOnly("native:BetterTags-1.20.1:1.1")
    modRuntimeOnly("native:IconRenderer-Forge-1.20.1:1.1.0")
    modRuntimeOnly("native:recipedumper:4.0-Forge")

    modCompileOnly("mezz.jei:jei-1.20.1-forge-api:15.2.0.25") { transitive false }
    modRuntimeOnly("mezz.jei:jei-1.20.1-forge:15.2.0.25") { transitive false }

    modImplementation "curse.maven:jade-324717:4801177"


    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = archives_base_name + "-" + project.name
            from components.java
        }
    }
}
